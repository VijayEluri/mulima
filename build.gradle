buildscript {
	repositories { jcenter() }
	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.6.0'
	}
}

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'scala'
apply plugin: 'application'
apply plugin: 'jacoco'
apply from: rootProject.file('gradle/license.gradle')

group = 'org.ajoberstar'
version = '0.2.0-SNAPSHOT'

applicationName = 'mulima'
mainClassName = 'org.mulima.cli.Main'

sourceCompatibility = '1.7'

repositories { jcenter() }

dependencies {
	// language deps
	compile 'org.scala-lang:scala-library:2.10.3'

	// logging deps
	compile 'org.slf4j:slf4j-api:1.7.5'
	runtime 'org.slf4j:jcl-over-slf4j:1.7.5'
	runtime 'ch.qos.logback:logback-classic:1.0.13'

	// spring
	compile 'org.springframework:spring-context:4.0.0.RELEASE'
	compile 'org.springframework:spring-jdbc:4.0.0.RELEASE'
	compile 'org.springframework:spring-tx:4.0.0.RELEASE'

	// testing framework
	testCompile 'org.scalatest:scalatest_2.10:2.0'
}

test {
	systemProperties 'log.level':'DEBUG'
}

startScripts {
	defaultJvmOpts = ['-Dlogback.configurationFile=@logbackConfig@']
}

applicationDistribution.filesMatching('**/mulima.bat') { details ->
	details.filter(ReplaceTokens, tokens:[logbackConfig: '%APP_HOME%\\config\\logback.xml'])
}
applicationDistribution.filesMatching('**/mulima') { details ->
	details.filter(ReplaceTokens, tokens:[logbackConfig: '$APP_HOME/config/logback.xml'])
}

wrapper {
	gradleVersion = '1.10'
}
