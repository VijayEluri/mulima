buildscript {
	repositories {
		jcenter()
		maven { url 'http://dl.bintray.com/ajoberstar/gradle-plugins' }
		maven { url 'http://dl.bintray.com/ajoberstar/temp-forks' }
	}
	dependencies {
		classpath 'org.ajoberstar:gradle-ajoberstar:0.2.0-milestone.1'
	}
}

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'scala'
apply plugin: 'application'
apply plugin: 'ajoberstar'

version = '0.2.0-SNAPSHOT'

ajoberstar {
	friendlyName = 'Mulima'
	description = 'Music Library Manager'
	copyrightYears = '2010-2014'
}

applicationName = 'mulima'
mainClassName = 'org.ajoberstar.mulima.cli.Main'

sourceCompatibility = '1.7'

repositories { jcenter() }

dependencies {
	// language deps
	compile 'org.scala-lang:scala-library:2.11.0'

	// logging deps
	compile 'org.slf4j:slf4j-api:1.7.7'
	runtime 'org.slf4j:jcl-over-slf4j:1.7.7'
	runtime 'ch.qos.logback:logback-classic:1.1.2'

	// spring
	compile 'org.springframework:spring-context:4.0.4.RELEASE'
	compile 'org.springframework:spring-jdbc:4.0.4.RELEASE'
	compile 'org.springframework:spring-tx:4.0.4.RELEASE'

	// testing framework
	testCompile 'org.scalatest:scalatest_2.11:2.1.6'
}

test {
	systemProperties 'log.level':'DEBUG'
}

startScripts {
	defaultJvmOpts = ['-Dlogback.configurationFile=@logbackConfig@']
}

applicationDistribution.filesMatching('**/mulima.bat') { details ->
	details.filter(ReplaceTokens, tokens:[logbackConfig: '%APP_HOME%\\config\\logback.xml'])
}
applicationDistribution.filesMatching('**/mulima') { details ->
	details.filter(ReplaceTokens, tokens:[logbackConfig: '$APP_HOME/config/logback.xml'])
}

wrapper {
	gradleVersion = '1.12'
}
